{
  Variáveis externas disponíveis:
    PathXsd: string;
    MappingXsdFileName: string;
    Input: string;
    Params: TStringList;
    Datasets: TspdXSDDataset;
}
var
  _Tx2: TStringList;
  _CamposMunicipio: TStringList;
  _inicio: Integer;
  _PadraoTecnoNFSe: Boolean;
  _linha: string;
  _NomeCampo: string;
  _ValorCampo: string;
  _PastaTecnoNFSe: string;
  _DsTecnoAtual: TSpdXSDClientDataSet;
  _DsCidadeAtual: TSpdXSDClientDataSet;
  _Vlr: Double;
  _cdsCidades: TClientDataSet;
  _QtdeRps: Integer;
  _Versao: String;
  DSCIDADE_NOTA, DSCIDADE_LOTE: string;

const
  DSTECNO_LOTE = 'TecnoNFSe';
  DSTECNO_NOTA = 'Rps';


{$I ..\Comuns\RotinasLerSetarCampos.txt}
{$I ..\Comuns\BuscarInfoCidade.txt}
{$I ..\Comuns\FormatarCodigoItemListaServico.txt}
{$I ..\Comuns\ConverterCodigoMunicipio.txt}
{$I ..\Comuns\EhComando.txt}
{$I ..\Comuns\CriarDatasets.txt}
{$I ..\Comuns\PrepararTecnoNFSeDatasets.txt}
{$I ..\Comuns\CarregarTX2.txt}
{$I ..\Comuns\AbreArquivos.txt}
{$I ..\Comuns\SetarCamposMunicipio.txt}
{$I ..\Comuns\FormataFloat.txt}
{$I ..\Comuns\Comuns.txt}

function isNaturezaOperacaoSetada : Boolean;
begin
  Result := Campo(_DsCidadeAtual, 'NaturezaOperacao').AsString <> '';
end;

function isIssRetido : Boolean;
begin
  Result := AnsiMatchText(LowerCase(CampoTecno('IssRetido')), ['sim', 'true', '1']);
end;


function EnvioSincrono: Boolean;
  begin
    Result := UpperCase(Params.Values['EnvioSincrono']) = 'TRUE';
  end;

begin
  Progress('Criando XSDDataset');
  CriarDatasets;

  _QtdeRps := 0;
  _Tx2 := TStringList.Create;
  _CamposMunicipio := TStringList.Create;
  _cdsCidades := TClientDataSet.Create(nil);

  try
    Progress('Carregando conteúdo/arquivo TX2');
    CarregarTX2;

    if _PadraoTecnoNFSe then
      _QtdeRps := VerificaOcorrenciaString('INCLUIRRPS',_Tx2.Text)
    else
      _QtdeRps := VerificaOcorrenciaString('INCLUIRITEM',_Tx2.Text);

    if (EnvioSincrono) and (_QtdeRps > 1) then
      RaiseException('Envio síncrono não suporta envio em Lote');

    _DsTecnoAtual := nil;

    if _PadraoTecnoNFSe then
    begin

      if EnvioSincrono then
      begin
        DSCIDADE_LOTE := 'GerarNfseEnvio';
        DSCIDADE_NOTA := 'GerarNfseEnvio';
      end
      else
      begin
        DSCIDADE_LOTE := 'EnviarLoteRpsEnvio';
        DSCIDADE_NOTA := 'Rps';
      end;

      Progress('Usando Padrão TecnoNFSe: preparando datasets');
      PrepararTecnoNFSeDatasets;

      AbreArquivos(_PastaTecnoNFSe + 'Cidades.db', _cdsCidades);

      // Passar dados para o Datasets, convertendo do padrão TecnoNFSe para o padrão da Cidade

      Datasets.GetDataSetByName(DSCIDADE_LOTE).Insert;

      // Começar com as notas
      Progress('Usando Padrão TecnoNFSe: carregando dados das notas');
      _DsTecnoAtual := TecnoNFSeDS.GetDataSetByName(DSTECNO_NOTA);
      _DsCidadeAtual := Datasets.GetDataSetByName(DSCIDADE_NOTA);

      _QtdeRps := _DsTecnoAtual.RecordCount;

      _DsTecnoAtual.First;

      while not _DsTecnoAtual.Eof do
      begin
        _DsCidadeAtual.Append;

        if CampoTecno('IdRps') <> '' then
          SetarCampo('IdRps')
        else
        begin
          _ValorCampo := 'RPS_' + CampoTecno('NumeroRps') + '_' + CampoTecno('SerieRps') + '_' + CampoTecno('TipoRps');
          SetarCampoValor('IdRps', _ValorCampo);
        end;

        SetarCampo('NumeroRps');
        SetarCampo('SerieRps');
        SetarCampo('TipoRps');

        _ValorCampo := FormatAnyDateTime(CampoTecno('DataEmissao'), XSDDATETIMEFORMAT);
        SetarCampoValor('DataEmissao', _ValorCampo);

        SetarCampo('OptanteSimplesNacional');
        SetarCampo('IncentivadorCultural');

        SetarCampoValor('Status', CampoTecno('SituacaoNota'));

        //Naturaza Operação --------------------------------------------------------------------------------------------

        if (Params.Values['Cidade'] = 'CACADOR') then
        begin
          case StrToIntDef(CampoTecno('TipoTributacao'), -1) of
            1 : SetarCampoValor('NaturezaOperacao', '3');
            2 : SetarCampoValor('NaturezaOperacao', '4');
            6 : SetarCampoValor('NaturezaOperacao', '1');
            7 : SetarCampoValor('NaturezaOperacao', '2');
          end;

          if (not isNaturezaOperacaoSetada) and (CampoTecno('RegimeEspecialTributacao') = '5') then
          begin
            SetarCampoValor('NaturezaOperacao', '13');
          end;

          if (not isNaturezaOperacaoSetada) and (CampoTecno('NaturezaTributacao') = '2') then
          begin
            SetarCampoValor('NaturezaOperacao', '10');
          end;

          if (not isNaturezaOperacaoSetada) and (CampoTecno('OptanteSimplesNacional') = '1') then
          begin
            if (isIssRetido) then
              SetarCampoValor('NaturezaOperacao', '14')
            else if (CampoTecno('MunicipioIncidencia') = '4203006') then
              SetarCampoValor('NaturezaOperacao', '11')
            else
              SetarCampoValor('NaturezaOperacao', '12');
          end;

          if (not isNaturezaOperacaoSetada) and (isIssRetido) then
          begin
            SetarCampoValor('NaturezaOperacao', '9');
          end;

          if (not isNaturezaOperacaoSetada)    then
           SetarCampoValor('NaturezaOperacao', '1');
        end
        else
        begin
          if (AnsiMatchText(CampoTecno('TipoTributacao'), ['101', '111', '121', '201', '301', '501', '511', '541', '551', '601', '701'])) then
             SetarCampoValor('NaturezaOperacao', CampoTecno('TipoTributacao'))
          else
          begin
            if (AnsiMatchText(CampoTecno('TipoTributacao'), ['1', '2', '3', '4', '5', '6', '7'])) then
            begin
              _ValorCampo := CampoTecno('NaturezaTributacao');

              //Caso a opção OptanteSimplesNacional seja '1 - Optante', a Natureza Operação é alterada
              if (CampoTecno('OptanteSimplesNacional') = '1') or (_ValorCampo = '1') then
              begin
                {Caso o Regime Especial Tributação seja '5 - MEI' e OptanteSimplesNacional seja '1 - Optante',
                o campo Natureza Operação será 541 - 'MEI (Simples Nacional)' }
                if (CampoTecno('RegimeEspecialTributacao') = '5') then
                  SetarCampoValor('NaturezaOperacao', '541')
                else
                begin
                  case StrToIntDef(CampoTecno('TipoTributacao'), -1) of
                    1, 2, 3, 4 : SetarCampoValor('NaturezaOperacao', '701');
                    5 : SetarCampoValor('NaturezaOperacao', '601');
                    6 : SetarCampoValor('NaturezaOperacao', '501');
                    7 : SetarCampoValor('NaturezaOperacao', '511');
                  end;
                end;
              end
              else
              begin
                //Caso a Natureza da Tributação seja '2 - Fixo', o tipo tributação é alterado para '121 - ISS Fixo'
                if _ValorCampo = '2' then
                  SetarCampoValor('NaturezaOperacao', '121')
                else
                begin
                  //Caso NaturezaTributacao seja qualquer outro caso (3, 4 ou 5) ou não for informada, a conversão ocorrerá conforme especificado.
                  case StrToIntDef(CampoTecno('TipoTributacao'), -1) of
                    1, 2, 3, 4 : SetarCampoValor('NaturezaOperacao', '301');
                    5 : SetarCampoValor('NaturezaOperacao', '201');
                    6 : SetarCampoValor('NaturezaOperacao', '101');
                    7 : SetarCampoValor('NaturezaOperacao', '111');
                  end;
                end;
              end;
            end
            else
              RaiseException('Valor inválido para o campo TipoTributacao. Valores aceitos: 1, 2, 3, 4, 6, 7 OU 101, 111, ' +
              '121, 201, 301, 501, 511, 541, 551, 601, 701.');
          end;
        end;
        //--------------------------------------------------------------------------------------------------------------

        SetarCampo('NumeroRpsSubstituido');
        SetarCampo('SerieRpsSubstituido');
        SetarCampo('TipoRpsSubstituido');

        SetarCampoCurrency('ValorServicos', True);
        SetarCampoCurrency('ValorDeducoes', False);
        SetarCampoCurrency('ValorPis', False);
        SetarCampoCurrency('ValorCofins', False);
        SetarCampoCurrency('ValorInss', False);
        SetarCampoCurrency('ValorIr', False);
        SetarCampoCurrency('ValorCsll', False);

        case AnsiIndexText(CampoTecno('IssRetido'), ['', '1', 'sim', 'true', '2', 'nao', 'não', 'false']) of
          0: // Vazio: deduzir do TipoTributacao
          begin
            if CampoTecno('TipoTributacao') = '5' then
              SetarCampoValor('IssRetido', '1')
            else
              SetarCampoValor('IssRetido', '2');
          end;
          1, 2, 3: SetarCampoValor('IssRetido', '1');
          4, 5, 6, 7: SetarCampoValor('IssRetido', '2');
        else
          RaiseException('Valor inválido para o campo IssRetido. Valores aceitos: 1, sim, true, 2, nao, não, false.');
        end;

        SetarCampoCurrency('ValorIss', False);

        if CampoTecno('ValorIssRetido') <> '' then
          SetarCampoCurrency('ValorIssRetido', False)
        else
        begin
          // Deduzível (se IssRetido=Sim, utiliza ValorIss)
          if AnsiMatchText(CampoTecno('IssRetido'), [ '1', 'sim', 'true']) then
            SetarCampoValor('ValorIssRetido', CampoCidade('ValorIss'));
        end;

        SetarCampoCurrency('OutrasRetencoes', False);
        SetarCampoCurrency('BaseCalculo', False);
        SetarCampoCurrency('ValorLiquidoNfse', False);
        SetarCampoCurrency('DescontoIncondicionado', False);
        SetarCampoCurrency('DescontoCondicionado', False);

        // Transformável: para Publica, 5% = 0.05
        if DivideCampoPorcentagem(CampoTecno('AliquotaISS')) then
        begin
          _Vlr := StrToFloat(StringReplace(CampoTecno('AliquotaISS'),',','.',[rfReplaceAll])) / 100;  // 5 --> 0,05
          _ValorCampo := FormataFloat(_Vlr, 5, 4);
          SetarCampoValor('Aliquota', _ValorCampo);
        end
        else
        begin
          RaiseException('Valor inválido para o campo AliquotaISS.');
        end;

        _ValorCampo := CampoTecno('CodigoItemListaServico');
        _ValorCampo := FormatarCodigoItemListaServico(_ValorCampo, False);  // Rio de Janeiro: sem ponto no ItemListaServico
        SetarCampoValor('ItemListaServico', _ValorCampo);

        SetarCampoValor('Discriminacao', CampoTecno('DiscriminacaoServico'));

        _ValorCampo := ConverterCodigoMunicipio(CampoTecno('CodigoCidadePrestacao'), True);
        SetarCampoValor('CodigoMunicipio', _ValorCampo);

        SetarCampoValor('CnpjPrestador', RemoveMascara(CampoTecno('CpfCnpjPrestador')));
        SetarCampoValor('InscricaoMunicipalPrestador', RemoveMascara(CampoTecno('InscricaoMunicipalPrestador')));// TODO: ver nº digitos

        _ValorCampo := RemoveMascara(CampoTecno('CpfCnpjTomador'));
        case Length(_ValorCampo) of
          11: SetarCampoValor('CpfTomador', _ValorCampo);
          14: SetarCampoValor('CnpjTomador', _ValorCampo);
        end;

        SetarCampo('RazaoSocialTomador');
        SetarCampoValor('InscricaoMunicipalTomador', RemoveMascara(CampoTecno('InscricaoMunicipalTomador')));// TODO: ver nº digitos
        SetarCampo('EnderecoTomador');
        SetarCampo('NumeroTomador');
        SetarCampo('ComplementoTomador');
        SetarCampo('BairroTomador');

        if CampoTecno('CodigoCidadeTomador') <> '' then
        begin
          _ValorCampo := ConverterCodigoMunicipio(CampoTecno('CodigoCidadeTomador'), True);
          SetarCampoValor('CodigoMunicipioTomador', _ValorCampo);

          if CampoTecno('UfTomador') <> '' then
            SetarCampo('UfTomador')
          else
          begin
            _ValorCampo := BuscarInfoCidade('CodIBGE', CampoCidade('CodigoMunicipioTomador'), 'UF');
            SetarCampoValor('UfTomador', _ValorCampo);
          end;
        end;

        SetarCampo('CepTomador');
        VerificaDDD('TelefoneTomador', 'TelefoneTomador', 'DDDTomador');
        SetarCampo('EmailTomador');

        SetarCampo('RazaoSocialIntermediario');
        _ValorCampo := RemoveMascara(CampoTecno('CpfCnpjIntermediario'));
        case Length(_ValorCampo) of
          0 : { Campo vazio, ignorar };
          11: SetarCampoValor('CpfIntermediario', _ValorCampo);
          14: SetarCampoValor('CnpjIntermediario', _ValorCampo);
        else
          RaiseException('Valor inválido para o campo CpfCnpjIntermediario');
        end;
        SetarCampoValor('InscricaoMunicipalIntermediario', RemoveMascara(CampoTecno('InscricaoMunicipalIntermediario')));// TODO: ver nº digitos

        _DsCidadeAtual.Post;

        _DsTecnoAtual.Next;
      end;

      SetarCamposMunicipio;
      // Atribuir os campos do lote
      if not(EnvioSincrono) then
      begin
        Progress('Usando Padrão TecnoNFSe: carregando dados do lote');
        _DsTecnoAtual := TecnoNFSeDS.GetDataSetByName(DSTECNO_LOTE);
        _DsCidadeAtual := Datasets.GetDataSetByName(DSCIDADE_LOTE);

        //SetarCampo('Versao');
        _Versao := '1.00';
        if CampoTecno('Versao') = '' then
          SetarCampoValor('versao', _Versao)
        else
          SetarCampo('versao');

        SetarCampo('NumeroLote');
        if CampoTecno('CpfCnpjRemetente') <> '' then
          SetarCampoValor('Cnpj', RemoveMascara(CampoTecno('CpfCnpjRemetente')))
        else
          SetarCampoValor('Cnpj', RemoveMascara(TecnoNFSeDS.GetDataSetByName(DSTECNO_NOTA).FieldByName('CpfCnpjPrestador').AsString));

        _ValorCampo := CampoTecno('InscricaoMunicipalRemetente');
        if _ValorCampo = '' then
          _ValorCampo := TecnoNFSeDS.GetDataSetByName(DSTECNO_NOTA).FieldByName('InscricaoMunicipalPrestador').AsString;

        SetarCampoValor('InscricaoMunicipal', RemoveMascara(_ValorCampo));

        if CampoTecno('QuantidadeRps') <> '' then
          SetarCampo('QuantidadeRps')
        else
          SetarCampoValor('QuantidadeRps', IntToStr(_QtdeRps));

        Datasets.GetDataSetByName(DSCIDADE_LOTE).Post;
      end;
    end
    else
    begin
      Datasets.LoadFromTx2Content(_Tx2.Text);
    end;
  finally
    _Tx2.Free;
    _CamposMunicipio.Free;
    _cdsCidades.Free;
    Progress('Arquivo TX2 carregado com Sucesso');
  end;
end.
