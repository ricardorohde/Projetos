{
  Variáveis externas disponíveis:
    PathXsd: string;
    MappingXsdFileName: string;
    Input: string;
    Params: TStringList;
    Datasets: TspdXSDDataset;
}
var
  _Tx2: TStringList;
  _CamposMunicipio: TStringList;
  _inicio: Integer;
  _PadraoTecnoNFSe: Boolean;
  _linha: string;
  _NomeCampo: string;
  _ValorCampo: string;
  _PastaTecnoNFSe: string;
  _DsTecnoAtual: TSpdXSDClientDataSet;
  _DsCidadeAtual: TSpdXSDClientDataSet;
  _DsServicoAtual: TSpdXSDClientDataSet;
  _Vlr: Double;
  _cdsCidades: TClientDataSet;
  _QtdeRps: Integer;
  DSCIDADE_NOTA: string;

const
  DSTECNO_LOTE = 'TecnoNFSe';
  DSTECNO_NOTA = 'Rps';
  DSCIDADE_LOTE = 'EnviarLoteRpsEnvio';
  DSCIDADE_SERV = 'ItensServico';

{$I ..\Comuns\RotinasLerSetarCampos.txt}
{$I ..\Comuns\BuscarInfoCidade.txt}
{$I ..\Comuns\FormatarCodigoItemListaServico.txt}
{$I ..\Comuns\ConverterCodigoMunicipio.txt}
{$I ..\Comuns\EhComando.txt}
{$I ..\Comuns\CriarDatasets.txt}
{$I ..\Comuns\PrepararTecnoNFSeDatasets.txt}
{$I ..\Comuns\CarregarTX2.txt}
{$I ..\Comuns\AbreArquivos.txt}
{$I ..\Comuns\SetarCamposMunicipio.txt}
{$I ..\Comuns\FormataFloat.txt}
{$I ..\Comuns\Comuns.txt}

function EnvioSincrono: Boolean;
begin
  Result := (UpperCase(Params.Values['EnvioSincrono'])) = 'TRUE';
end;

procedure SetarCampoValorServicoSimpliss(const aCampo, aValor: string);
begin
  Campo(_DsServicoAtual, aCampo).AsString := aValor;
end;

procedure SetarCampoCurrencyServSimpliss(const aCampoSimpliss, aCampoTecno: string; const SetarSeZero: boolean);
var _Valor: Currency;
begin
  _Valor := AnyCurrencyToCurrency(CampoTecno(aCampoTecno));
  if (_Valor <> 0) or SetarSeZero then
    Campo(_DsServicoAtual, aCampoSimpliss).AsString :=
      CurrencyToXsdCurrency(_Valor);
end;

procedure SetarCampoSeAssinc(const aCampo: string);
begin
  if not EnvioSincrono then
    SetarCampo(aCampo);
end;

procedure SetarCampoValorSeAssinc(const aCampo, aValor: string);
begin
  if not EnvioSincrono then
    SetarCampoValor(aCampo, aValor);
end;

begin
  Progress('Criando XSDDataset');
  CriarDatasets;

  _Tx2 := TStringList.Create;
  _CamposMunicipio := TStringList.Create;
  _cdsCidades := TClientDataSet.Create(nil);

  try
    Progress('Carregando conteúdo/arquivo TX2');
    CarregarTX2;

    _DsTecnoAtual := nil;

    if _PadraoTecnoNFSe then
    begin
      if EnvioSincrono then
        DSCIDADE_NOTA := 'GerarNovaNfseEnvio'
      else
        DSCIDADE_NOTA := 'Rps';

      Progress('Usando Padrão TecnoNFSe: preparando datasets');
      PrepararTecnoNFSeDatasets;

      AbreArquivos(_PastaTecnoNFSe + 'Cidades.db', _cdsCidades);

      // Passar dados para o Datasets, convertendo do padrão TecnoNFSe para o padrão da Cidade
      if not EnvioSincrono then
        Datasets.GetDataSetByName(DSCIDADE_LOTE).Insert;

      // Começar com as notas
      Progress('Usando Padrão TecnoNFSe: carregando dados das notas');
      _DsTecnoAtual := TecnoNFSeDS.GetDataSetByName(DSTECNO_NOTA);
      _DsCidadeAtual := Datasets.GetDataSetByName(DSCIDADE_NOTA);
      _DsServicoAtual := Datasets.GetDataSetByName(DSCIDADE_SERV);

      _QtdeRps := _DsTecnoAtual.RecordCount;

      if EnvioSincrono and (_QtdeRps > 1) then
        raiseException('Envio de Lote Síncrono não suportado para o padrão SIMPLISS');

      _DsTecnoAtual.First;

      while not _DsTecnoAtual.Eof do
      begin
        _DsCidadeAtual.Append;

        if CampoTecno('IdRps') <> '' then
          SetarCampo('IdRps')
        else
        begin
          _ValorCampo := 'RPS_' + CampoTecno('NumeroRps') + '_' + CampoTecno('SerieRps') + '_' + CampoTecno('TipoRps');
          SetarCampoValor('IdRps', _ValorCampo);
        end;

        SetarCampoSeAssinc('NumeroRps');
        SetarCampoSeAssinc('SerieRps');
        SetarCampoSeAssinc('TipoRps');

        _ValorCampo := FormatAnyDateTime(CampoTecno('DataEmissao'), XSDDATETIMEFORMAT);
        SetarCampoValorSeAssinc('DataEmissao', _ValorCampo);

        _ValorCampo := CampoTecno('Competencia');
        if EnvioSincrono and (_ValorCampo <> '') then
        begin
          _ValorCampo := FormatAnyDateTime(_ValorCampo, XSDDATEFORMAT);
          SetarCampoValor('Competencia', _ValorCampo);
        end;

        SetarCampo('OptanteSimplesNacional');
        SetarCampo('IncentivadorCultural');

        SetarCampoValor('Status', CampoTecno('SituacaoNota'));

        case StrToIntDef(CampoTecno('TipoTributacao'), -1) of
          1, 3, 4 : SetarCampoValor('NaturezaOperacao', '3');
          2 : SetarCampoValor('NaturezaOperacao', '4');
          5 : RaiseException('Valor inválido para o campo TipoTributacao. Para ABRASF o TipoTributacao não pode ser "5 - Retida". Para isso pode se utilizar "6 - Tributavel dentro do municipio" ou "7 - Tributável fora do município');
          6 : SetarCampoValor('NaturezaOperacao', '1');
          7 : SetarCampoValor('NaturezaOperacao', '2');
        else
          RaiseException('Valor inválido para o campo TipoTributacao. Valores aceitos: 1, 2, 3, 4, 6, 7.');
        end;

        _ValorCampo := CampoTecno('TipoTributacao');
        // NaturezaTributacao: ignorar quando TipoTributacao for diferente de 6 ou 7
        if AnsiMatchText(_ValorCampo, ['6', '7']) then
        begin
          _ValorCampo := CampoTecno('NaturezaTributacao');
          case StrToIntDef(CampoTecno('NaturezaTributacao'), -1) of
            1: ;// IGNORADO
            2, 3: SetarCampoValor('NaturezaOperacao', '1');
            4: SetarCampoValor('NaturezaOperacao', '5');
            5: SetarCampoValor('NaturezaOperacao', '6');
          end;
        end;

        if (AnsiMatchText(CampoTecno('RegimeEspecialTributacao'), ['1', '2', '3', '4', '5', '6'])) then
          SetarCampo('RegimeEspecialTributacao')
        else if (AnsiMatchText(CampoTecno('RegimeEspecialTributacao'), ['7', '8'])) then
          SetarCampoValor('RegimeEspecialTributacao', '7');

        SetarCampoSeAssinc('NumeroRpsSubstituido');
        SetarCampoSeAssinc('SerieRpsSubstituido');
        SetarCampoSeAssinc('TipoRpsSubstituido');

        SetarCampoCurrency('ValorServicos', True);
        SetarCampoCurrency('ValorDeducoes', False);
        SetarCampoCurrency('ValorPis', False);
        SetarCampoCurrency('ValorCofins', False);
        SetarCampoCurrency('ValorInss', False);
        SetarCampoCurrency('ValorIr', False);
        SetarCampoCurrency('ValorCsll', False);

        case AnsiIndexText(CampoTecno('IssRetido'), ['', '1', 'sim', 'true', '2', 'nao', 'não', 'false']) of
          0: // Vazio: deduzir do TipoTributacao
          begin
            if CampoTecno('TipoTributacao') = '5' then
              SetarCampoValor('IssRetido', '1')
            else
              SetarCampoValor('IssRetido', '2');
          end;
          1, 2, 3: SetarCampoValor('IssRetido', '1');
          4, 5, 6, 7: SetarCampoValor('IssRetido', '2');
        else
          RaiseException('Valor inválido para o campo IssRetido. Valores aceitos: 1, sim, true, 2, nao, não, false.');
        end;

        SetarCampoCurrency('ValorIss', False);

        if CampoTecno('ValorIssRetido') <> '' then
          SetarCampoCurrency('ValorIssRetido', True)
        else
        begin
          // Deduzível (se IssRetido=Sim, utiliza ValorIss)
          if AnsiMatchText(CampoTecno('IssRetido'), [ '1', 'sim', 'true']) then
            SetarCampoValor('ValorIssRetido', CampoCidade('ValorIss'));
        end;

        SetarCampoCurrency('OutrasRetencoes', False);
        SetarCampoCurrency('BaseCalculo', False);
        SetarCampoCurrency('ValorLiquidoNfse', False);
        SetarCampoCurrency('DescontoIncondicionado', False);
        SetarCampoCurrency('DescontoCondicionado', False);

        // Transformável: para Simpliss, 5% = 0.05
        if DivideCampoPorcentagem(CampoTecno('AliquotaISS')) then
        begin
       _ValorCampo := StringReplace(CampoTecno('AliquotaISS'),',','.',[rfReplaceAll]);
            
          if not AnsiMatchText(Params.Values['Cidade'], ['PRESIDENTEPRUDENTE', 'VOLTAREDONDARJ']) then 
          begin
            _Vlr := StrToFloat(_ValorCampo);  //Bug #2287, retirado divisão por 100;
            _ValorCampo := FormataFloat(_Vlr, 5, 4);
          end;

          SetarCampoValor('Aliquota', _ValorCampo);
        end
        else
        begin
          RaiseException('Valor inválido para o campo AliquotaISS.');
        end;

        _ValorCampo := CampoTecno('CodigoItemListaServico');

        SetarCampoValor('ItemListaServico', _ValorCampo);

        SetarCampo('CodigoCnae');
        SetarCampo('CodigoTributacaoMunicipio');

        SetarCampoValor('Discriminacao', CampoTecno('DiscriminacaoServico'));

        _DsServicoAtual.Edit;

        SetarCampoValorServicoSimpliss('Quantidade',CampoTecno('QuantidadeServicos'));
        if CampoTecno('QuantidadeServicos') = '' then
          SetarCampoValorServicoSimpliss('Quantidade',IntToStr(_QtdeRps));

        SetarCampoCurrencyServSimpliss('ValorUnitario','ValorUnitarioServico',False);
        if CampoTecno('ValorUnitarioServico') = '' then
          SetarCampoCurrencyServSimpliss('ValorUnitario','ValorServicos',False);

        _ValorCampo := CampoTecno('DescricaoRPS');
        if _ValorCampo = '' then
            _ValorCampo := CampoTecno('DiscriminacaoServico');
        if Length(_ValorCampo) > 100 then
        begin
            _ValorCampo := Copy(_ValorCampo, 1,100);
            SetarCampoValorServicoSimpliss('Descricao', _ValorCampo)
        end
        else
            SetarCampoValorServicoSimpliss('Descricao', _ValorCampo);

        _DsServicoAtual.Post;


        _ValorCampo := ConverterCodigoMunicipio(CampoTecno('CodigoCidadePrestacao'), True);
        SetarCampoValor('CodigoMunicipio', _ValorCampo);

        SetarCampoValor('CnpjPrestador', RemoveMascara(CampoTecno('CpfCnpjPrestador')));
        SetarCampoValor('InscricaoMunicipalPrestador', RemoveMascara(CampoTecno('InscricaoMunicipalPrestador')));// TODO: ver nº digitos

        _ValorCampo := RemoveMascara(CampoTecno('CpfCnpjTomador'));
        case Length(_ValorCampo) of
          11: SetarCampoValor('CpfTomador', _ValorCampo);
          14: SetarCampoValor('CnpjTomador', _ValorCampo);
        else
          RaiseException('Valor inválido para o campo CpfCnpjTomador');
        end;

        SetarCampo('RazaoSocialTomador');
        SetarCampoValor('InscricaoMunicipalTomador', RemoveMascara(CampoTecno('InscricaoMunicipalTomador')));// TODO: ver nº digitos
        SetarCampo('EnderecoTomador');
        SetarCampo('NumeroTomador');
        SetarCampo('ComplementoTomador');
        SetarCampo('BairroTomador');

        _ValorCampo := ConverterCodigoMunicipio(CampoTecno('CodigoCidadeTomador'), True);
        SetarCampoValor('CodigoMunicipioTomador', _ValorCampo);

        if CampoTecno('UfTomador') <> '' then
          SetarCampo('UfTomador')
        else
        begin
          _ValorCampo := BuscarInfoCidade('CodIBGE', CampoCidade('CodigoMunicipioTomador'), 'UF');
          SetarCampoValor('UfTomador', _ValorCampo);
        end;

        SetarCampo('CepTomador');
        VerificaDDD('TelefoneTomador', 'TelefoneTomador', 'DDDTomador');
        SetarCampo('EmailTomador');

        SetarCampo('RazaoSocialIntermediario');
        _ValorCampo := RemoveMascara(CampoTecno('CpfCnpjIntermediario'));
        case Length(_ValorCampo) of
          0 : { Campo vazio, ignorar };
          11: SetarCampoValor('CpfIntermediario', _ValorCampo);
          14: SetarCampoValor('CnpjIntermediario', _ValorCampo);
        else
          RaiseException('Valor inválido para o campo CpfCnpjIntermediario');
        end;
        SetarCampoValor('InscricaoMunicipalIntermediario', RemoveMascara(CampoTecno('InscricaoMunicipalIntermediario')));// TODO: ver nº digitos


        _DsCidadeAtual.Post;
        _DsTecnoAtual.Next;
      end;

      SetarCamposMunicipio;
      // Atribuir os campos do lote
      if not(EnvioSincrono) then
      begin
        Progress('Usando Padrão TecnoNFSe: carregando dados do lote');
        _DsTecnoAtual := TecnoNFSeDS.GetDataSetByName(DSTECNO_LOTE);
        _DsCidadeAtual := Datasets.GetDataSetByName(DSCIDADE_LOTE);

        // IdLote é deduzível do NumeroLote
        if CampoTecno('IdLote') <> '' then
          SetarCampo('IdLote')
        else
        begin
          _ValorCampo := 'LOTE_' + CampoTecno('NumeroLote');
          SetarCampoValor('IdLote', _ValorCampo);
        end;

        SetarCampo('NumeroLote');
        if CampoTecno('CpfCnpjRemetente') <> '' then
          SetarCampoValor('Cnpj', RemoveMascara(CampoTecno('CpfCnpjRemetente')))
        else
          SetarCampoValor('Cnpj', RemoveMascara(TecnoNFSeDS.GetDataSetByName(DSTECNO_NOTA).FieldByName('CpfCnpjPrestador').AsString));

        _ValorCampo := CampoTecno('InscricaoMunicipalRemetente');
        if _ValorCampo = '' then
          _ValorCampo := TecnoNFSeDS.GetDataSetByName(DSTECNO_NOTA).FieldByName('InscricaoMunicipalPrestador').AsString;
        SetarCampoValor('InscricaoMunicipal', RemoveMascara(_ValorCampo));

        if CampoTecno('QuantidadeRps') <> '' then
          SetarCampo('QuantidadeRps')
        else
          SetarCampoValor('QuantidadeRps', IntToStr(_QtdeRps));

        Datasets.GetDataSetByName(DSCIDADE_LOTE).Post;
     end;
    end
    else
    begin
      if EnvioSincrono and (VerificaOcorrenciaString('IdRps', _Tx2.Text) > 1) then
        raiseException('Envio de Lote Síncrono não suportado para o padrão SIMPLISS')
      else
        Datasets.LoadFromTx2Content(_Tx2.Text);
    end;
  finally
    _Tx2.Free;
    _CamposMunicipio.Free;
    _cdsCidades.Free;
    Progress('Arquivo TX2 carregado com Sucesso');
  end;
end.
